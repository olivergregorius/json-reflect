plugins {
    id "java"
    id "java-library"

    id "jacoco"
    id "org.sonarqube" version "3.3"

    id "maven-publish"
    id "signing"
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

def snapshotVersion = "0.1.0-SNAPSHOT"
def springVersion = "5.3.19"
def springBootVersion = "2.6.7"
def slf4jVersion = "1.7.36"
def lombokVersion = "1.18.24"
def gsonVersion = "2.9.0"
def jmesPathVersion = "0.5.1"
def hamcrestVersion = "2.2"
def commonsLang3Version = "3.12.0"

sourceCompatibility = JavaVersion.VERSION_17
group = "dev.gregorius.library"
version = project.hasProperty("tagName") ? tagName.replaceAll("^v", "") : snapshotVersion

repositories {
    mavenCentral()
}

dependencies {
    // Spring and Spring Boot
    implementation "org.springframework:spring-web:$springVersion"
    testImplementation "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
    testImplementation "org.springframework.boot:spring-boot-starter-web:$springBootVersion"

    // Logging
    implementation "org.slf4j:slf4j-api:$slf4jVersion"

    // Lombok
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    // GSON
    api "com.google.code.gson:gson:$gsonVersion"
    implementation "io.burt:jmespath-gson:$jmesPathVersion"

    // Hamcrest
    implementation "org.hamcrest:hamcrest:$hamcrestVersion"

    // Util
    implementation "org.apache.commons:commons-lang3:$commonsLang3Version"
}

java {
    withJavadocJar()
    withSourcesJar()
}

def testCoverageExludes = []

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
    jacoco {
        excludes += testCoverageExludes
    }
}

jacocoTestReport {
    dependsOn("test")
    reports {
        html.required = true
        xml.required = true
    }
    afterEvaluate {
        classDirectories.setFrom(
            fileTree(dir: "build/classes/java/main", excludes: testCoverageExludes)
        )
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "olivergregorius_json-reflect"
        property "sonar.organization", "olivergregorius"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.projectDir}/build/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.coverage.exclusions", testCoverageExludes.join(",")
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'JSON REFlecT'
                description = 'JSON REST Fluent economic Testing'
                url = 'https://github.com/olivergregorius/json-reflect'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/olivergregorius/json-reflect/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'olivergregorius'
                        name = 'Oliver Gregorius'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/olivergregorius/json-reflect.git'
                    developerConnection = 'scm:git:https://github.com/olivergregorius/json-reflect.git'
                    url = 'https://github.com/olivergregorius/json-reflect'
                }
            }
        }
    }
}


signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

tasks.register("releaseCheck") {
    def semanticVersionPattern = ~/^\d+\.\d+\.\d+$/
    doFirst {
        // Check if release is performed in the pipeline
        if (!project.hasProperty("ci") || !ci) {
            throw new InvalidUserDataException("Release must be performed in the pipeline!")
        }

        // Check if the provided version complies with semantic versioning
        if (!semanticVersionPattern.matcher(project.version).matches()) {
            throw new InvalidUserDataException("Provided version does not comply with semantic versioning!")
        }
    }
}

tasks.register("releaseToMavenCentral") {
    dependsOn("releaseCheck")
    dependsOn("publishToSonatype")
    dependsOn("closeAndReleaseSonatypeStagingRepository")
}

afterEvaluate {
    tasks.named("publishToSonatype") {
        mustRunAfter("releaseCheck")
    }
    tasks.named("closeAndReleaseSonatypeStagingRepository") {
        mustRunAfter("publishToSonatype")
    }
}
